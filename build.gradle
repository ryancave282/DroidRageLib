plugins {
    id 'java'
    id "edu.wpi.first.GradleRIO" version "2025.1.1"
    id "maven-publish"

}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    archiveClassifier.set('javadoc')
    from tasks.javadoc
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ryancave282/DroidRageLib")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) { // Name of the publication
            from(components.java) // Include Java components (e.g., classes, resources)
            artifact(tasks.named('sourcesJar').get())  // Updated syntax
            artifact(tasks.named('javadocJar').get())  // Updated syntax

            // Optionally define additional metadata
            pom {
                name.set("DroidRageLib")
                description.set("A library for Android development.")
                url.set("https://github.com/ryancave282/droidragelib")
                licenses {
                    license {
                        name.set("MIT License")
                        url.set("https://opensource.org/licenses/MIT")
                    }
                }
                developers {
                    developer {
                        id.set("ryancave282")
                        name.set("Ryan Cueva")
                        email.set("ryancueva282@gmail.com")
                    }
                }
                scm {
                    connection.set("scm:git:git://github.com/ryancave282/droidragelib.git")
                    developerConnection.set("scm:git:ssh://github.com/ryancave282/droidragelib.git")
                    url.set("https://github.com/ryancave282/droidragelib")
                }
            }
        }
    }
}

group = 'net.droidrage'
version = '1.0.1'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

}

test {
    useJUnitPlatform()
}
